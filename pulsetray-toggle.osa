#!/usr/bin/osascript

-- Configure these:
set CONNECTION_NAME to "Springtown"
set CONNECTION_INDEX to 5  --  position in the list of connections (1-based)


tell application "PulseTray"
    set STATUS to connectionStatus of connection CONNECTION_INDEX
end tell
-- return STATUS

tell application "System Events" to tell process "PulseTray"
    activate

    -- UI elements
    -- ^ https://n8henrie.com/2013/03/a-strategy-for-ui-scripting-in-applescript/

    tell menu bar item 1 of menu bar 2
        click
        tell menu item CONNECTION_NAME of menu 1
            click
            delay 0.1
            if STATUS = "Disconnected"
                click menu item "Connect" of menu 1
            else
                click menu item "Disconnect" of menu 1
            end if
        end tell
    end tell
end tell

if STATUS = "Disconnected"
    delay 2
    set TOKEN to do shell script "/usr/local/bin/stoken"
    tell application "System Events" to tell process "PulseTray"
        tell sheet 1 of window 1
            click button "Proceed"
            delay 1
            set value of text field 2 to TOKEN
            click button "Connect"
        end tell
    end tell
end if
